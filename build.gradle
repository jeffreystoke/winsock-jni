group 'com.github.jeffreystoke'
version '0.0.1-alpha'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'cpp'

buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

model {
    components {
        winsock_jni(NativeLibrarySpec) {
            sources {
                cpp {
                    lib library: 'winsock', linkage: 'api'
                    lib library: 'jni', linkage: 'api'
                    source {
                        srcDir 'src/main/jni'
                        include "**/*.cpp"
                    }
                }
            }
        }
    }
    
    platforms {
        x64 { architecture "x86_64" }
        x86 { architecture "x86" }
    }
    toolChains {
        visualCpp(VisualCpp) {
            // Specify the installDir if Visual Studio cannot be located
            // installDir "C:/Apps/Microsoft Visual Studio 10.0"
            // installDir "C:/Program Files (x86)/Microsoft Visual Studio 14.0"
        }
    }
    repositories {
        libs(PrebuiltLibraries) {
            winsock {
                headers.srcDir "C:/Program Files (x86)/Windows Kits/8.1/Include/um"
            }
            jni {
                def JAVA_HOME = "${System.properties.'java.home'}/.."
                headers.srcDir "${JAVA_HOME}/include"
                headers.srcDir "${JAVA_HOME}/include/win32"
            }
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}
